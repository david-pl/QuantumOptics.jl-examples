{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using QuantumOptics\n",
    "using DifferentialEquations\n",
    "import Base: ==\n",
    "\n",
    "\n",
    "###### FROM MODULE #####\n",
    "\n",
    "const QuantumState = Union{Ket, DenseOperator}\n",
    "\n",
    "type State{T<:QuantumState}\n",
    "    quantum::T\n",
    "    classical::Vector{Complex128}\n",
    "end\n",
    "\n",
    "Base.length(state::State) = length(state.quantum) + length(state.classical)\n",
    "Base.copy(state::State) = State(copy(state.quantum), copy(state.classical))\n",
    "\n",
    "function =={T<:QuantumState}(a::State{T}, b::State{T})\n",
    "    samebases(a.quantum, b.quantum) &&\n",
    "    length(a.classical)==length(b.classical) &&\n",
    "    (a.classical==b.classical) &&\n",
    "    (a.quantum==b.quantum)\n",
    "end\n",
    "\n",
    "operators.expect(op, state::State) = expect(op, state.quantum)\n",
    "\n",
    "# Modified master equation solver\n",
    "function master_stochastic(tspan, state0::State{DenseOperator}, fquantum, fdeterm, fstoch;\n",
    "                solver=EulerHeun(),\n",
    "                rates::Union{Vector{Float64}, Matrix{Float64}, Void}=nothing,\n",
    "                fout::Union{Function,Void}=nothing,\n",
    "                tmp::DenseOperator=copy(state0.quantum))\n",
    "    tspan_ = convert(Vector{Float64}, tspan)\n",
    "    function dmaster_(t, state::State{DenseOperator}, dstate::State{DenseOperator})\n",
    "        dmaster_h_determ(t, state, fquantum, fdeterm, rates, dstate, tmp)\n",
    "    end\n",
    "    x0 = Vector{Complex128}(length(state0))\n",
    "    recast!(state0, x0)\n",
    "    state = copy(state0)\n",
    "    dstate = copy(state0)\n",
    "    integrate(tspan_, dmaster_, fstoch, x0, state, dstate, solver)\n",
    "end\n",
    "\n",
    "# Modified integrate function\n",
    "function integrate(tspan_, dmaster_determ, fstoch, x0, state, dstate, solver)\n",
    "  function df_(t, x::Vector{Complex128}, dx::Vector{Complex128})\n",
    "   recast!(x, state)\n",
    "   recast!(dx, dstate)\n",
    "   dmaster_determ(t, state, dstate)\n",
    "   recast!(dstate, dx)\n",
    "  end\n",
    "  prob = SDEProblem(df_, fstoch, x0, (tspan_[1], tspan_[end]))\n",
    "  sol = solve(prob, solver)\n",
    "  rho_out = []\n",
    "  tmp = State(DenseOperator(state.quantum.basis_l, state.quantum.basis_r), Vector{Complex128}(length(state.classical)))\n",
    "  for i=1:length(sol.t)\n",
    "    push!(rho_out, copy(recast!(sol.u[i], tmp)))\n",
    "  end\n",
    "  return sol.t, rho_out\n",
    "end\n",
    "\n",
    "function recast!(state::State, x::Vector{Complex128})\n",
    "    N = length(state.quantum)\n",
    "    copy!(x, 1, state.quantum.data, 1, N)\n",
    "    copy!(x, N+1, state.classical, 1, length(state.classical))\n",
    "    x\n",
    "end\n",
    "\n",
    "function recast!(x::Vector{Complex128}, state::State)\n",
    "    N = length(state.quantum)\n",
    "    copy!(state.quantum.data, 1, x, 1, N)\n",
    "    copy!(state.classical, 1, x, N+1, length(state.classical))\n",
    "    state\n",
    "end\n",
    "\n",
    "# Modified dmaster\n",
    "function dmaster_h_determ(t::Float64, state::State{DenseOperator}, fquantum, fdeterm, rates, dstate::State{DenseOperator}, tmp::DenseOperator)\n",
    "    fquantum_(t, rho) = fquantum(t, state.quantum, state.classical)\n",
    "    timeevolution.timeevolution_master.dmaster_h_dynamic(t, state.quantum, fquantum_, rates, dstate.quantum, tmp)\n",
    "    fdeterm(t, state.quantum, state.classical, dstate.classical)\n",
    "end\n",
    "\n",
    "\n",
    "######### EXAMPLE ########\n",
    "\n",
    "# Parameters\n",
    "Nc = 16\n",
    "κ = 1.0\n",
    "g = 0.5κ\n",
    "γ = 2κ\n",
    "Δa = -0.5γ\n",
    "Δc = 0.0\n",
    "η = 1.0\n",
    "ξ = 0.1 # Noise strength\n",
    "m = 3.33\n",
    "tlist = [0:0.1:100;]\n",
    "\n",
    "# Basis\n",
    "bc = FockBasis(Nc)\n",
    "ba = SpinBasis(1//2)\n",
    "\n",
    "a = destroy(bc) ⊗ one(ba)\n",
    "aᵗ = create(bc) ⊗ one(ba)\n",
    "σ = one(bc) ⊗ sigmam(ba)\n",
    "σᵗ = one(bc) ⊗ sigmap(ba)\n",
    "\n",
    "H0 = -Δc*aᵗ*a - Δa*σᵗ*σ + η*(a + aᵗ)\n",
    "Hx = g*(aᵗ*σ + a*σᵗ) # ∝ cos(x)\n",
    "J = [sqrt(κ)*a, sqrt(γ)*σ]\n",
    "Jdagger = dagger.(J)\n",
    "\n",
    "function fquantum(t, ψ, u)\n",
    "  H0 + Hx*cos(real(u[1])), J, Jdagger\n",
    "end\n",
    "\n",
    "atσ = aᵗ*σ\n",
    "function fdeterm(t, ψ, u, du)\n",
    "  du[1] = u[2]/m\n",
    "  du[2] = 2g*sin(real(u[1]))*real(expect(atσ, ψ))\n",
    "end\n",
    "\n",
    "function fstoch(t, u, du)\n",
    "  du[end] = ξ\n",
    "end\n",
    "\n",
    "x0 = sqrt(2)\n",
    "p0 = 4\n",
    "u0 = Complex128[x0, p0]\n",
    "ρ0 = State(dm(fockstate(bc, 0) ⊗ spindown(ba)), u0)\n",
    "\n",
    "tout, ρt = master_stochastic(tlist, ρ0, fquantum, fdeterm, fstoch; solver=SRI())\n",
    "\n",
    "x = [r.classical[1] for r=ρt]\n",
    "p = [r.classical[2] for r=ρt]\n",
    "\n",
    "pe = expect(σᵗ*σ, ρt)\n",
    "pg = expect(σ*σᵗ, ρt)\n",
    "\n",
    "n = expect(aᵗ*a, ρt)\n",
    "\n",
    "using PyPlot\n",
    "subplot(221)\n",
    "plot(tout, p)\n",
    "\n",
    "subplot(222)\n",
    "plot(tout, x)\n",
    "\n",
    "subplot(223)\n",
    "plot(tout, pe)\n",
    "\n",
    "subplot(224)\n",
    "plot(tout, n)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.0",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
